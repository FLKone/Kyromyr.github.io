:global string cfcm
:local int i
:local double temp

#machineIndex gig("cf.Digit" . sub(cfcm, i, 1))
#machineCount gig("cfcm." . {machineIndex})
#machineName gsg("cfm." . {machineIndex})
#machineItem gig("cfcm." . {machineIndex} . "." . {machineCount})
#craftItem gig("cfcm.0." . gig("cfcm.0"))
#itemAmount gdg("cfc." . {machineItem})
#itemName gsg("cf." . {machineItem} . ".n")
#itemTier gig("cf." . {machineItem} . ".t")
#produceName gsg("cf." . gig("cf." . {machineItem} . ".0.i") . ".n")
#craftsRemain gig("cfcm.0") + gig("cfcm.1") + gig("cfcm.2") + gig("cfcm.3") + gig("cfcm.4") + gig("cfcm.5") + gig("cfcm.6") + gig("cfcm.7") + gig("cfcm.8") + gig("cfcm.9")

loop:
	i = (i + 1) % len(cfcm)
	goto(if({machineCount} == 0 | active({machineName}) | ({machineItem} > {craftItem} + 21*3), loop, if({machineIndex} > 0, produce, craft)))
	produce:
		temp = min({itemAmount}, count({produceName}, {itemTier}))
		produce({produceName}, {itemTier}, temp, {machineName})
		gds("cfc." . {machineItem}, {itemAmount} - temp)
		goto(if({itemAmount} == 0., next, loop))
	craft:
		temp = count({itemName}, {itemTier})
		craft({itemName}, {itemTier}, {itemAmount})
		gds("cfc." . {machineItem}, {itemAmount} + temp - count({itemName}, {itemTier}))
		goto(if({itemAmount} == 0., next, loop))
	next:
		gis("cfcm." . {machineIndex}, {machineCount} - 1)
		cfcm = if({machineCount} > 0, cfcm, sub(cfcm, 0, i) . sub(cfcm, i+1, 99))
		goto(if({craftsRemain} == 0, 99, loop))